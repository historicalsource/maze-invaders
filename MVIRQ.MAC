	.TITLE MVIRQ - MAZE INVADERS IRQ
	.NLIST
	.INCLUDE MVDEF
	.LIST
	.PAGE
	.CSECT	MVIRQ
CNTINT	=0			;WE WILL COUNT INTERRUPTS
INCLUD	=1			;THIS FILE IS INCLUDED
COIN67	=1			;COINS ARE ON D7,D6,D5
COIN	=0			;COIN SWITCHES ARE ACTIVE LOW
SLAM	=0			;SLAM SWITCH IS ACTIVE LOW
EMCTRS	=3			;3 COIN COUNTERS
BONADD	=1			;BONUS ADDERS ARE IMPLEMENTED
	.INCLUDE COIN65		;<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
	.SBTTL	COS/SIN - 8 BIT COSINE/SINE ROUTINES
;COS - ED LOGG		4/03/81
;
;ENTRY	(A)=ANGLE (0 TO FF)
;EXIT	(A)=COSINE(-127+0127)
;	(Y)=SIGN EXTENSION
;	(CC)=MINUS IF RESULT IS NEGATIVE
;	(CC)=ZERO IF RESULT IS ZERO
;USES	A,X,Y
;CALLS	COMP,SIN
COS:	CLC			;COS(A)=SIN(A+PI/2)
	ADC I,40
;	JMP SIN


;SIN - ED LOGG		4/03/81
;
;ENTRY	(A)=ANGLE (0 TO FF REPRESENTS 0 TO 360 DEGREES)
;	(CC)=MINUS/PLUS FLAG SET CORRECTLY
;EXIT	(A)=SINE (-127 TO +127)
;	(Y)=SIGN EXTENSION
;	(CC)=MINUS IF RESULT IS NEGATIVE
;	(CC)=ZERO IF RESULT IS ZERO
;USES	A,X,Y
;CALLS	COMP
SIN:	BPL SIN1		;IF PI > ANGLE >-0
	AND I,7F
	JSR SIN1		;SIN(A) WHEN PI > A >= 0
	LDY I,0
	JSR COMP		;SIN(PI+A)=-SIN(A)
	BPL 10$
	DEY			;Y=FF
10$:	RTS

SIN1:	CMP I,41
	BCC 10$			;PI/2 >- ANGLE >- 0
	EOR I,7F		;SIN(PI/2+A)=SIN(PI/2-A)
	ADC I,0			;ADD 1
10$:	TAX
	LDY I,0
	LDA AX,99$
	BPL 20$			;SIGN EXTEND
	DEY			;Y=FF
20$:	RTS

	.RADIX 10.
99$:	.BYTE 0,3,6,9,12,16,19,22		;SIN(0 TO 7)
	.BYTE 25,28,31,34,37,40,43,46
	.BYTE 49,51,54,57,60,63,65,68
	.BYTE 71,73,76,78,81,83,85,88
	.BYTE 90,92,94,96,98,100,102,104
	.BYTE 106,107,109,111,112,113,115,116
	.BYTE 117,118,120,121,122,122,123,124
	.BYTE 125,125,126,126,126,127,127,127	;SIN(0-63)
	.BYTE 127				;SIN(64)
	.RADIX 16
	.PAGE

MESG:	.WORD ENGLIS
	.WORD GERMAN
	.WORD FRENCH
	.WORD SPANIS

ENGLIS:	.WORD MES0
	.WORD MES1
	.WORD MES2
	.WORD MES3
	.WORD MES4
	.WORD MES5
	.WORD MES6
	.WORD MES7
	.WORD MES8
	.WORD MES9
	.WORD MES10
	.WORD MES11
	.WORD MES12
	.WORD MES13
	.WORD MES14
	.WORD MES15
	.WORD MES16
	.WORD MES17
	.WORD MES18
	.WORD MES19
	.WORD MES20
	.WORD MES21
	.WORD MES22
	.WORD MES23
	.WORD MES24
	.WORD MES25
	.WORD MES26
	.WORD MES27
	.WORD MES28
	.WORD MES29
	.WORD MES30
	.WORD MES31
	.WORD MES32
	.WORD MES33
	.WORD MES34

GERMAN:	.WORD MES0G
	.WORD MES1G
	.WORD MES2G
	.WORD MES3G
	.WORD MES4G
	.WORD MES5G
	.WORD MES6G
	.WORD MES7G
	.WORD MES8G
	.WORD MES9G
	.WORD MES10G
	.WORD MES11G
	.WORD MES12
	.WORD MES13G
	.WORD MES14G
	.WORD MES15G
	.WORD MES16G
	.WORD MES17G
	.WORD MES18G
	.WORD MES19G
	.WORD MES20G
	.WORD MES21G
	.WORD MES22G
	.WORD MES23G
	.WORD MES24G
	.WORD MES25G
	.WORD MES26G
	.WORD MES27G
	.WORD MES28G
	.WORD MES29G
	.WORD MES30G
	.WORD MES31G
	.WORD MES32G
	.WORD MES33G
	.WORD MES34G

FRENCH:	.WORD MES0F
	.WORD MES1F
	.WORD MES2F
	.WORD MES3F
	.WORD MES4F
	.WORD MES5F
	.WORD MES6F
	.WORD MES7F
	.WORD MES8F
	.WORD MES9
	.WORD MES10F
	.WORD MES11F
	.WORD MES12
	.WORD MES13F
	.WORD MES14
	.WORD MES15F
	.WORD MES16
	.WORD MES17F
	.WORD MES18F
	.WORD MES19F
	.WORD MES20F
	.WORD MES21F
	.WORD MES22F
	.WORD MES23F
	.WORD MES24F
	.WORD MES25F
	.WORD MES26
	.WORD MES27F
	.WORD MES28F
	.WORD MES29F
	.WORD MES30F
	.WORD MES31F
	.WORD MES32F
	.WORD MES33F
	.WORD MES34F

SPANIS:	.WORD MES0S
	.WORD MES1S
	.WORD MES2S
	.WORD MES3S
	.WORD MES4S
	.WORD MES5S
	.WORD MES6S
	.WORD MES7S
	.WORD MES8S
	.WORD MES9S
	.WORD MES10S
	.WORD MES11S
	.WORD MES12S
	.WORD MES13S
	.WORD MES14S
	.WORD MES15S
	.WORD MES16S
	.WORD MES17S
	.WORD MES18S
	.WORD MES19S
	.WORD MES20S
	.WORD MES21S
	.WORD MES22S
	.WORD MES23S
	.WORD MES24S
	.WORD MES25S
	.WORD MES26S
	.WORD MES27S
	.WORD MES28S
	.WORD MES29S
	.WORD MES30S
	.WORD MES31S
	.WORD MES32S
	.WORD MES33S
	.WORD MES34S


	.MACRO	ASCIN A,C
	.WORD	A
	.ASCIN	/C/
	.ENDM

	.NLIST	MEB
MES0:	ASCIN 16D,<PLAYER >
MES0G:	ASCIN 16D,<SPIELER >
MES0F:	ASCIN 16D,<JOUEUR >
MES0S:	ASCIN 16D,<JUGADOR >
MES1:	ASCIN 111,<1 COIN 2 PLAYS>
MES1G:	ASCIN 0F1,<1 MUENZE 2 SPIELE>
MES1F:	ASCIN 0F1,<1 PIECE 2 JOUEURS>
MES1S:	ASCIN 0F1,<1 FICHA 2 JUEGOS>
MES2:	ASCIN 111,<1 COIN 1 PLAY>
MES2G:	ASCIN 0F1,<1 MUENZE 1 SPIEL>
MES2F:	ASCIN 0F1,<1 PIECE 1 JOUEUR>
MES2S:	ASCIN 0F1,<1 FICHA 1 JUEGO>
MES3:	ASCIN 111,<2 COINS 1 PLAY>
MES3G:	ASCIN 0F1,<2 MUENZEN 1 SPIEL>
MES3F:	ASCIN 0F1,<2 PIECES 1 JOUEUR>
MES3S:	ASCIN 0F1,<2 FICHAS 1 JUEGO>
MES4:	ASCIN 8A,<CONTINUE PREVIOUS GAME>
MES4G:	ASCIN 6A,<LETZTES SPIEL FORTFUEHREN>
MES4F:	ASCIN 2A,<CONTINUEZ LA DERNIERE PARTIE>
MES4S:	ASCIN 8A,<CONTINUAR ULTIMO JUEGO>
MES5:	ASCIN 0A9,<ENTER YOUR INITIALS>
MES5G:	ASCIN 29,<GEBEN SIE IHRE INITIALEN EIN>
MES5F:	ASCIN 089,<ENTREZ VOS INITIALES>
MES5S:	ASCIN 089,<ENTRE SUS INICIALES>
MES6:	ASCIN 88,<EXTRA LIFE EVERY >
MES6G:	ASCIN 0A8,<EXTRA LEBEN BEI >
MES6F:	ASCIN 68,<NOUVELLE VIE CHAQUE >
MES6S:	ASCIN 88,<VIDA EXTRA CADA >
MES7:	ASCIN 15B,<HIGH SCORES>
MES7G:	ASCIN 0FB,<HOECHSTERGEBNISSE>
MES7F:	ASCIN 0FB,<MEILLEURS SCORES>
MES7S:	ASCIN 19B,<RECORDS>
MES8:	ASCIN 12A,<GREAT SCORE>
MES8G:	ASCIN 8A,<GROSSARTIGES ERGEBNIS>
MES8F:	ASCIN 0EA,<SPLENDIDE SCORE>
MES8S:	ASCIN 10A,<GRAN PUNTAJE>
MES9:	ASCIN 14D,<CREDITS >
MES9G:	ASCIN 14D,<KREDITE >
MES9S:	ASCIN 14D,<CREDITOS >
MES10:	ASCIN 0EF,<2 CREDIT MINIMUM>
MES10G:	ASCIN 6F,<GELDEINWURF FUER 2 SPIELE>
MES10F:	ASCIN 10F,<2 JEUX MINIMUM>
MES10S:	ASCIN 0EF,<2 JUEGOS MINIM0>
MES11:	ASCIN 101,<SPECIAL BONUS>
MES11G:	ASCIN 101,< SPEZIALBONUS  >
MES11F:	ASCIN 101,<BONUS SPECIAL>
MES11S:	ASCIN 101,<PREMIO ESPECIAL>
MES12:	ASCIN 301,<BONUS>
MES12S:	ASCIN 301,<PREMIO>
MES13:	ASCIN 21,<MAZE>
MES13G:	ASCIN 1,<ZIMMER>
MES13F:	ASCIN 1,<DEDALE>
MES13S:	ASCIN 1,<CUADRO>
MES14:	ASCIN 101,< TRIPLE BONUS>
MES14G:	ASCIN 101,<3 FACHER BONUS>
MES14S:	ASCIN 101,< TRIPLE PREMIO >
MES15:	ASCIN 0AB,<SELECT STARTING MAZE>
MES15G:	ASCIN 12B,<WAEHLE ZIMMER>
MES15F:	ASCIN 8B,<CHOISSISEZ VOTRE DEDALE>
MES15S:	ASCIN 6B,<SELECCIONE CUADRO INICIAL>
MES16:	ASCIN 1E0,<POINTS>
MES16G:	ASCIN 1E0,<PUNKTE>
MES16S:	ASCIN 1E0,<PUNTOS>
MES17:	ASCIN 19A,<TIME >
MES17G:	ASCIN 19A,<ZEIT >
MES17F:	ASCIN 19A,<DUREE >
MES17S:	ASCIN 17A,<TIEMPO >
MES18:	ASCIN 131,<PREVIOUS GAME>
MES18G:	ASCIN 131,<LETZTES SPIEL>
MES18F:	ASCIN 111,<DERNIERE PARTIE>
MES18S:	ASCIN 131,<ULTIMO JUEGO>
MES19:	ASCIN 0A9,<PUSH BUTTON TO START>
MES19G:	ASCIN 69,<KNOPF DRUECKEN FUER START>
MES19F:	ASCIN 89,<APPUYEZ POUR COMMENCER>
MES19S:	ASCIN 69,<OPRIMA BOTON DE COMIENZO>
MES20:	ASCIN 0A6,<ENTER >
MES20G:	ASCIN 0A6,<NOCH >
MES20F:	ASCIN 86,<ENTREZ >
MES20S:	ASCIN 66,<ENTRAR >
MES21:	ASCIN 1C6,<GREEN DOORS>
MES21G:	ASCIN 1A6,<GRUENE TUEREN>
MES21F:	ASCIN 1C6,<PORTES VERTES>
MES21S:	ASCIN 1A6,<PUERTAS VERDES>
MES22:	ASCIN 0E5,<FOR 20000 POINTS>
MES22G:	ASCIN 0E5,<FUER 20000 PUNKTE>
MES22F:	ASCIN 0E5,<POUR 20000 POINTS>
MES22S:	ASCIN 0E5,<PARA 20000 PUNTOS>
MES23:	ASCIN 0C9,<NEW LIFE AT >
MES23G:	ASCIN 89,<NEUER LEBEN BEI >
MES23F:	ASCIN 49,<NOUVELLE VIE CHAQUE >
MES23S:	ASCIN 0C9,<NUEVO VIDA A >
MES24:	ASCIN 1B6,<IN MAZE >
MES24G:	ASCIN 196,<IM ZIMMER >
MES24F:	ASCIN 196,<DANS LE DEDALE >
MES24S:	ASCIN 1B6,<EN EL CUADRO >
MES25:	ASCIN 0B6,<NEXT>
MES25G:	ASCIN 0B6,<NUN>
MES25F:	ASCIN 36,<SUIVANT>
MES25S:	ASCIN 056,<PROXIMO>
MES26:	ASCIN 155,<10000 POINTS>
MES26G:	ASCIN 155,<10000 PUNKTE>
MES26S:	ASCIN 155,<10000 PUNTOS>
MES27:	ASCIN 172,<CONTINUE>
MES27G:	ASCIN 152,<FORTFUEHREN>
MES27F:	ASCIN 172,<CONTINUEZ>
MES27S:	ASCIN 172,<CONTINUAR>
MES28:	ASCIN 66,<MAZE>
MES28G:	ASCIN 46,<ZIMMER>
MES28F:	ASCIN 46,<DEDALE>
MES28S:	ASCIN 46,<CUADRO>
MES29:	ASCIN 7A,<MAZE>
MES29G:	ASCIN 5A,<ZIMMER>
MES29F:	ASCIN 5A,<DEDALE>
MES29S:	ASCIN 5A,<CUADRO>
MES30:	ASCIN 2E6,<MAZE>
MES30G:	ASCIN 2C6,<ZIMMER>
MES30F:	ASCIN 2C6,<DEDALE>
MES30S:	ASCIN 2C6,<CUADRO>
MES31:	ASCIN 2FA,<MAZE>
MES31G:	ASCIN 2DA,<ZIMMER>
MES31F:	ASCIN 2DA,<DEDALE>
MES31S:	ASCIN 2DA,<CUADRO>
MES32:	ASCIN 170,<MAZE >
MES32G:	ASCIN 170,<ZIMMER >
MES32F:	ASCIN 170,<DEDALE >
MES32S:	ASCIN 170,<CUADRO >
MES33:	ASCIN 10B,<INSERT COIN TO>
MES33G:	ASCIN 0CB,<MUENZE EINWERFEN UM>
MES33F:	ASCIN 0AB,<METTEZ UNE PIECE POUR>
MES33S:	ASCIN 0CB,<INSERTE FICHA PARA>
MES34:	ASCIN 10B,<PRESS START TO>
MES34G:	ASCIN 8B,<STARTKNOPF DRUECKEN UM>
MES34F:	ASCIN 0AB,< APPUYEZ START POUR  >
MES34S:	ASCIN 0AB,<OPRIMA COMIENZO PARA>

	.LIST	MEB
	.PAGE
	.SBTTL	MESS-MESSAGE ROUTINE
;MESS-MESSAGE ROUTINE
;
;ENTRY  (A)=BITS 0-5 MESSAGE NUMBER (0,1,2,...)
;	    BIT 7=1 TO BLANK MESSAGE
;	    BIT 7=0 TO WRITE MESSAGE
;EXIT	(TEMP4,TEMP4+1)=ADDRESS OF DESTINATION
;USES	A,X,Y,(TEMP1,TEMP1+1),(TEMP4,TEMP4+1),(TEMP5,TEMP5+1)
;CALLS	CHAR
MESS:	ASL
	ROR TEMP1+1		;SAVE BLANK/WRITE FLAG
	STA TEMP1
	LDA OPTNS2
	AND I,03		;LANGUAGE BITS
	ASL
	TAX
	LDA X,MESG		;GET ADDRESS OF CORRECT LANGUAGE
	STA TEMP4
	LDA X,MESG+1
	STA TEMP4+1
	LDY TEMP1
	LDA NY,TEMP4
	STA TEMP5		;GET ADDRESS OF MESSAGE
	INY
	LDA NY,TEMP4
	STA TEMP5+1
	LDY I,0
	LDA NY,TEMP5		;GET MESSAGE POSITION
	TAX
	INY
	LDA NY,TEMP5
	BIT CKIND
	BPL 2$			;SAVE PARAMETERS
	STA TEMP4+1
	STX TEMP4
	LDA I,0BF		;3BF-POSITION=FLIPPED POSITION
	SEC
	SBC TEMP4
	TAX
	LDA I,03
	SBC TEMP4+1
2$:	ORA I,PLYFLD/100
	STX TEMP4
	STA TEMP4+1
	INY			;PROPER POSITION OF INDEX
;***************************************
;
;THIS POINT ON DOWN SHOULD BE WITHIN THE
;LAST 4K (ONE 2732) OF THE PROGRAM SPACE.
;
;*****************************************
5$:	STY TEMP1
10$:	LDY TEMP1
	LDA NY,TEMP5
	AND I,3F		;GET RID OF SIGN BIT IF ANY
	CMP I,20
	BEQ 20$			;IF A BLANK, USE ZERO
	LDX TEMP1+1
	BPL 30$			;IF WRITE MESSAGE
20$:	LDA I,0			;BLANK MESSAGE
30$:	CMP I,30
	BCC 40$			;IF LETTER
	AND I,2F		;IF A NUMBER (MAP 31 TO 21, 32 TO 22,...)
40$:	JSR CHAR		;PUT OUT CHARACTER AND UPDATE (TEMP4,TEMP4+1)
45$:	LDY TEMP1
	INC TEMP1
	LDA NY,TEMP5
	BPL 10$			;IF MORE CHARACTERS TO COME
	RTS
	
MESSAG	=.
	LDY I,0	
	STY TEMP1+1		;DO NOT ERASE MESSAGE
	BEQ 5$			;ALWAYS

	.PAGE


	.SBTTL ABS/COMP
;ABS/COM-ABSOLUTE VALUE AND COMPLEMENT
;
;ENTRY	(A)=NUMBER
;	(CC)=NEGATIVE IF (A) IS NEGATIVE
;EXIT	(A)=RESULT
ABS:	BPL 10$			;IF POSITIVE
COMP	=.
	EOR I,0FF
	CLC
	ADC I,01
10$:	RTS



.SBTTL	CHAR-PRINT CHARACTER
;CHAR-PRINT ONE CHARACTER ON SCREEN
;
;ENTRY (TEMP4,TEMP4+1)=SCREEN POSITION
;      (A)=CHARACTER
;	(HILITE)=HIGH LIGHT FOR CHARACTERS (D6 AND D7)
;EXIT (TEMP4,TEMP4+1)=NEXT SCREEN POSITION
;

;INITIAL-DISPLAY A PLAYER'S INITIAL
;
;ENTRY(Y)=INDEX INTO INITL TABLE
;     (TEMP4,TEMP4+1=SCREEN POSITION
;EXIT (TEMP4,TEMP4+1)=NEXT SCREEN POSITION
;USES	A,Y,(TEMP4,TEMP4+1)
INITAL:	LDA Y,INITL
CHAR:	LDY I,0
	EOR HILITE		;ADD COLOR HILIGHT
	STA NY,TEMP4
SKIP:	LDA I,20
	LDA CKFF		;20 OR-20 DEPENDNG ON COCKTAIL
	ORA I,20
	AND I,0E0
	CLC
	ADC TEMP4
	STA TEMP4
	LDA CKFF
	ADC TEMP4+1
	STA TEMP4+1
	RTS


	.SBTTL DIGITS
;
;ENTRY (A)=2 DIGITS OF SCORE
;	(TEMP4)=ADDRESS AT WHICH SCORE IS TO BE DISLAYED
;	(CC)=CARRY CLEAR IF NO ZERO SUPPRESSION
;	(HILITE)=COLOR HIGHLIGHT (D6 AND D7)
;EXIT	(TEMP4,TEMP4+1)=NEXT SCREEN ADDRESS
;	(CC)=CARRY CLEAR IF NO ZERO SUPPRESSION
;CALLS	 CHAR
;USES	A,Y,(TEMP4,TEMP4+1)
DIGIT2: PHA
	PHP
	LSR
	LSR
	LSR
	LSR
	PLP
	JSR DIGITZ		;PUT OUT A DIGIT
	PLA
	AND I,0F
DIGITZ:	BCC 10$			;IF NO ZERO SUPPRESSION
	AND I,0F
	BEQ 20$			;SUPPRESS THIS ZERO AND LEAVE CARRY SET
10$:	CLC
	ORA I,20		;DIGITS ARE 20-29
20$:	PHP
	CMP I,2A
	BCC 30$			;IF A NUMBER
	SBC I,29		;LETTERS START AT 1
30$:	JSR CHAR		;PUT OUT DIGIT
	PLP
	RTS


	.SBTTL	JOYSTK - CONVERT JOYSTICK READINGS
;
; ENTRY	(CKIND)=DETERMINES CURRENT ACTIVE JOYSTICK
;	(LOW)=LOW POT READINGS
;	(HIGH)=HIGH POT READINGS
;	(LAST)=LAST POT READINGS
; EXIT	(JOYS)=RANGE OF 0 TO 0F FOR HORIZONTAL POT
;	(JOYS+2)=RANGE OF 0 TO 0F FOR VERTICAL POT
; USES	A,X,Y,(TEMP1,TEMP1+1)

JOYSTK:	LDX I,0
	BIT CKIND
	BPL 10$			;IF NOT IN PLAYER 2 COCKTAIL
	INX
10$:	JSR JOYST1		;DO HORIZONTAL
	INX
	INX			;DO VERTICAL
JOYST1:	LDA X,HIGH
	SEC
	SBC X,LOW
	STA TEMP1+1		;THIS CANNOT BE ZERO (DIVISOR)
	LDA X,LAST
	SEC
	SBC X,LOW		;WE NOW HAVE DIVIDEND
	BCS 5$			;NO UNDERFLOW
	LDA I,0
5$:	LDY I,0
	STY TEMP1		;PREPARE RESULT
	LDY I,4			;NUMBER OF DIVIDE CYCLES
	CLC			;CLEAR MSB OF RESULT
10$:	ROL TEMP1
	ROL
	CMP TEMP1+1
	BCC 20$			;NOT LARGE ENOUGH
	SBC TEMP1+1
20$:	DEY
	BNE 10$
	LDA TEMP1
	ROL			;SHIFT IN LAST BIT
	STA X,JOYS
	LDA MODE
	BNE 25$			;IF NOT IN GAME PLAY
	LDA FRAME+1
	AND I,03
	ORA FRAME
	BNE 25$			;EVERY 16 SECONDS
	INC X,LOW
	DEC X,HIGH
25$:	RTS

	.SBTTL IRQ-IRQ PROCESSING
IRQ:	PHA			;SAVE REGISTERS
	TXA
	PHA
	TYA
	PHA
	CLD			;CLEAR DECIMAL MODE
	INC $INTCT		;INCREMENT FRAME COUNTER
	BIT VBLANK
	BPL 4$			;IF NOT VBLANK
	LDA $INTCT
	AND I,0FC
	STA $INTCT		;SYNC VBLANK WITH COUNTER
4$:	LDA $INTCT
	AND I,03
	TAX
	STA X,POTW		;SET WHICH POT WE WANT
	STA X,POTW		;YES - THERE MUST BE TWO STORES
	BIT SLFTST
	BPL 6$			;IF IN SELF TEST-NO SLAM ALARM
	CMP I,02
	BNE 5$			;NOT IN MIDDLE OF THE SCREEN
	LDX I,NMULT-1
	JSR MALIEN		;DISPLAY NUMTIPLEXED ALIEN
5$:	LDA $LMTIM
	BEQ 6$			;IF NO ABUSE
	LDA I,10		;EAR PIERCING SCREECH
	STA AUDF0
	STA AUDF1
	LDA I,0AF		;CONSTANT TONE AND MAX VOLUME
	STA AUDC0
	STA AUDC1
6$:	BIT VBLANK
	BPL 50$			;NOT IN VBLANK
	INC SYNC		;WE ARE IN NEXT FRAME
	INC FRAME		;UPDATE FRAME COUNTER
	BNE 8$			;IF NO OVERFLOW
	INC FRAME+1
	SED
	LDA GTIME		;INCREMENT GAME TIMER
	CLC
	ADC I,1
	STA GTIME
	LDA GTIME+1
	ADC I,0			;40000 SECONDS=666 MINUTES=12 HOURS
	BCS 7$			;NO OVERFLOW
	STA GTIME+1
7$:	CLD
8$:	LDA SYNC
	CMP I,8
10$:	BCS 10$			;OOPS-WATCHDOG WILL RESET US.
	LDA $$CRDT
	CMP I,37.
15$:	BCS 15$			;OOPS-LET WATCHDOG RESET MS
	CMP I,19.
	BCC 20$			;IF SMALL # OF CREDITS
	LDA I,18.
	STA $$CRDT		;SET MAX NUMBER OF CREDITS
20$:	LDX I,15.
	BIT SLFTST
	BPL 40$			;IF IN SELF TEST MULTIPLEX ALL OBJECTS
30$:	LDA X,MOBJP		;MOVE PICTURE PARAMETERS TO HARDWARE REGISTERS
	STA X,MOP
	LDA X,MOBJV
	STA X,MOV
	LDA X,MOBJH
	STA X,MOH
	LDA X,MOBJC
	STA X,MOC
	DEX
	CPX I,NMULT
	BCS 30$			;LOOP THRU ALL OBJECTS
40$:	DEX
	JSR MALIEN		;DISPLAY MULTIPLEXED ALIENS
50$:	BIT SLFTST
	BMI 55$			;IF NOT SELF-TEST
	LDA COLCNT
	BMI 90$			;IF NOT IN COLOR BAR TEST
	INC COLCNT
	BIT VBLANK
	BPL 52$			;IF NOT VBLANK
	LDA I,0
	STA COLCNT
52$:	LDA COLCNT
	ASL			;0,2,4 OR 6
	ASL			;0,4,8 OR 12.
	TAX
	STX ANCOL
	INX
	STX ANCOL+6
	INX
	STX ANCOL+8
	INX
	STX ANCOL+0E
	BPL 90$			;ALWAYS-SKIP OVER COIN COUNTER CHECK

55$:	LDX I,2
60$:	LDA X,$CCTIM		;SET COIN COUNTERS
	STA X,CCNTR
	DEX
	BPL 60$
	LDA $INTCT
	EOR BONUST		;DO IT DURING REAL PROCESSING
	BNE 90$			;NOT TOO OFTEN
SECUR0	=.			;SECURITY CHECK #0
	LDX I,14.		;NUMBER OF BYTES TO CHECKSUM
	LDA I,0EF		;EXPECTED CHECKSUM
65$:	EOR X,CPYRHT		;CHECKSUM THE COPYRIGHT MESSAGE
	DEX
	BPL 65$
	TAX
	BEQ 90$			;IF CHECKSUM IS OK
	TSX
	LDA X,104
	EOR I,0C3		;CHANGE ALL FOUR FLAGS
	STA X,104		;SET FLAGS TO JUNK
90$:	JSR MOOLAH		;READ COINS-KILLS TIME FOR POTR
	LDA $INTCT
	AND I,03
	TAX			;100 US MINIMUM
	LDA POTR		;READ THE CURRENT POT
	LDY X,HIGH
	BNE 91$			;IF POTS HAVE BEEN READ BEFORE
	STA X,HIGH		;INITIALIZE POT VALUES
	STA X,LOW
	STA X,LAST
91$:	TAY
	SEC
	SBC X,LAST
	JSR ABS
	CMP I,02
	BCC 92$			;IF CLOSE TO LAST VALUE IGNORE CURRENT
	TYA
	STA X,LAST
92$:	LDA X,LAST
	CMP X,LOW
	BCS 93$			;IF VALUE ABOVE LOWEST READING
	DEC X,LOW
93$:	CMP X,HIGH
	BCC 95$			;IF WE DO NOT
	INC X,HIGH		;THIS IS ONE MORE THAN HIGHEST VALUE
95$:	STA IRQACK		;ACKNOWLEDGE IRQ
	PLA			;RESTORE REGISTERS
	TAY
	PLA
	TAX
	PLA
	RTI			;RETURN FROM IRQ


	.SBTTL	MALIEN - DISPLAY MULTIPLEXED ALIEN
;
;ENTRY	(X)=INDEX OF CURRENT ALIEN ENTRY
;USES	A,X,Y
MALIEN:	TXA
	AND I,0FE
	TAY			;Y IS THE INDEX INTO MOTION OBJECT REGISTERS
	LDA X,MOBJP		;SET PICTURE CODE
	STA Y,MOP
	PHA
	AND I,3F
	CMP I,20
	PLA
	BCC 10$			;IF NOT AN ALIEN PICTURE
	EOR I,0D0		;REFLECTION FOR TOP PICTURE
10$:	EOR I,10
	STA Y,MOP+1
	LDA X,MOBJV		;SET V POSITION
	STA Y,MOV
	STA Y,MOV+1
	LDA X,MOBJH		;SET H POSITION
	STA Y,MOH
	CLC
	ADC I,08
	STA Y,MOH+1
	LDA X,MOBJC		;SET COLORS
	STA Y,MOC
	STA Y,MOC+1
	DEX
	DEX
	BPL MALIEN		;REPEAT FOR ALL ALIENS
	RTS

	.END
                       